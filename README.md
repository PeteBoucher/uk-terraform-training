# hello-terraform

This repository has a very basic Terraform hello world with Azure. It creates a VM with NGINX running and basic resources to have a public connection to it, the project uses a set of Azure credentials from sandbox Resource Group and Service Principle.

The `vm.tf` module contains all the necessary Terraform code to create the Virtual Machine and public connection to port 80 and 22 (for SSH into the VM).

The `azure.tf` module contains the Azure provider configuration using the secrets of the Resource Group and Service Principle to be used by the rest Terraform modules.

The `variables.tf` module contains basic and reusable variables to avoid complexity and magic strings in `vm.tf`.

Since this is a repository for exercise purposes we kept thing as simple as possible, so this project is using local Terraform state, which means that your local machine will contain the "source of truth" of Terraform and it should be the only machine applying resources to the Sandbox Resource Group.

### Prerequisites
- Terraform `v0.12.8`
- Azure CLI `2.0.59` or latest

Run `terraform init` command inside the repository to initialize the Terraform working directory.

Run `terraform --version` to print the Terraform and the Azure provider version. You should see something similiar to this:
```sh
Terraform v0.12.8
+ provider.azurerm v1.28.0
```

Run `terraform plan` command to create an execution plan and see what is going to the created, changed and deleted.

Run `terraform apply` command to apply the changes required to reach the desired state of the configuration, or the pre-determined set of actions generated by a terraform plan execution plan.

In order to use Azure CLI to visualize the resources you will create you `must` install the local client on your machine, please follow the guidelines at:
https://docs.microsoft.com/en-us/cli/azure/install-azure-cli?view=azure-cli-latest

Once you have the Azure CLI installed you `must` login using the same Service Principals credentials you have, they are the same for Terraform.
```sh
export RESOURCE_GROUP_NAME=<resourceGroup>
export SP_APP_ID=<appId>
export SP_SECRET=<password>
export TENANT_ID=<tenant>
az login --service-principal --username $SP_APP_ID --password $SP_SECRET --tenant $TENANT_ID
```

Run `az resource list --resource-group $RESOURCE_GROUP_NAME` at anytime to see all the resources created at your Resource Group, it's good to run after you've applied something.

## VM with NGINX running on a Public Ip

At the `vm.tf` module add the following block of code to create the network:
```sh
resource "azurerm_virtual_network" "hello-terraform" {
  name                = "${var.prefix}-network"
  address_space       = ["10.0.0.0/16"]
  location            = var.location
  resource_group_name = data.azurerm_resource_group.main.name
  tags                = var.tags
}
```
Now run the `terraform plan` and then `terraform apply` to create your network.

### This is the current setup
![This is the current setup](/img/vm-example1.jpg)

At the `vm.tf` module add the following block of code to create the subnet for your network:
```sh
resource "azurerm_subnet" "internal-subnet" {
  name                 = "${var.prefix}-internal"
  resource_group_name  = data.azurerm_resource_group.main.name
  virtual_network_name = azurerm_virtual_network.hello-terraform.name
  address_prefix       = "10.0.2.0/24"
}
```
Now run the `terraform plan` and then `terraform apply` to create your network.

### This is the current setup
![This is the current setup](/img/vm-example2.jpg)

At the `vm.tf` module add the following block of code to create the private NIC inside the internal subnet:
```sh
resource "azurerm_network_interface" "hello-terraform-private" {
  name                = "${var.prefix}-privateNIC"
  location            = var.location
  resource_group_name = data.azurerm_resource_group.main.name
  ip_configuration {
    name                          = "testconfiguration1"
    subnet_id                     = azurerm_subnet.internal-subnet.id
    private_ip_address_allocation = "Dynamic"
  }
  tags = var.tags
}
```
Now run the `terraform plan` and then `terraform apply` to create your network.

At the `vm.tf` module add the following block of code to create the Ubuntu VM using the private NIC:
```sh
resource "azurerm_virtual_machine" "hello-terraform" {
  name                = "${var.prefix}-vm"
  location            = var.location
  resource_group_name = data.azurerm_resource_group.main.name

  network_interface_ids = [azurerm_network_interface.hello-terraform-private.id] #1. example with vm using the private NIC
  # network_interface_ids            = [azurerm_network_interface.hello-terraform-private.id, azurerm_network_interface.hello-terraform-public.id] #2. example with vm using the public NIC
  # primary_network_interface_id     = azurerm_network_interface.hello-terraform-public.id                                                         #2. example with vm using the public NIC
  vm_size                          = "Standard_DS1_v2"
  delete_os_disk_on_termination    = true
  delete_data_disks_on_termination = true
  storage_image_reference {
    publisher = "Canonical"
    offer     = "UbuntuServer"
    sku       = "16.04-LTS"
    version   = "latest"
  }
  storage_os_disk {
    name              = "${var.prefix}-vmDisk"
    caching           = "ReadWrite"
    create_option     = "FromImage"
    managed_disk_type = "Standard_LRS"
  }
  os_profile {
    computer_name  = "hostname"
    admin_username = var.admin_user
    admin_password = var.admin_password
  }
  os_profile_linux_config {
    disable_password_authentication = false
  }
  tags = var.tags
}
```
Now run the `terraform plan` and then `terraform apply` to create your network.

### This is the current setup
![This is the current setup](/img/vm-example4.jpg)

At the `vm.tf` module add the following block of code to create a public IP:
```sh
resource "azurerm_public_ip" "hello-terraform" {
  name                = "${var.prefix}-publicIp"
  location            = var.location
  resource_group_name = data.azurerm_resource_group.main.name
  allocation_method   = "Static"
  tags                = var.tags
}

output "hello_terraform_public_ip" {
  value = azurerm_public_ip.hello-terraform.ip_address
}
```
Now run the `terraform plan` and then `terraform apply` to create your network, pay attention to the output of the apply, it will show something like this

```sh
Apply complete! Resources: 2 added, 0 changed, 0 destroyed.

Outputs:

hello_terraform_public_ip = 104.47.139.60
```
This `hello_terraform_public_ip` address is the Public IP you just allocated for your VM, save it because it will be useful soon.

At the `vm.tf` module add the following block of code to create the a network security group that allows SSH connections from * on the 22 port:
```sh
resource "azurerm_network_security_group" "hello-terraform" {
  name                = "${var.prefix}-networkSecurityGroup"
  location            = var.location
  resource_group_name = data.azurerm_resource_group.main.name
  tags                = var.tags
}

resource "azurerm_network_security_rule" "SSH" {
  name                        = "SSH"
  priority                    = 1001
  direction                   = "Inbound"
  access                      = "Allow"
  protocol                    = "Tcp"
  source_port_range           = "*"
  destination_port_range      = "22"
  source_address_prefix       = "*"
  destination_address_prefix  = "*"
  resource_group_name         = data.azurerm_resource_group.main.name
  network_security_group_name = azurerm_network_security_group.hello-terraform.name
}
```
Now run the `terraform plan` and then `terraform apply` to create your network.

At the `vm.tf` module add the following block of code to create a public NIC using the Public Ip:
```sh
resource "azurerm_network_interface" "hello-terraform-public" {
  name                      = "${var.prefix}-publicNIC"
  location                  = var.location
  resource_group_name       = data.azurerm_resource_group.main.name
  network_security_group_id = azurerm_network_security_group.hello-terraform.id
  ip_configuration {
    name                          = "nicConfiguration"
    subnet_id                     = azurerm_subnet.internal-subnet.id
    private_ip_address_allocation = "Dynamic"
    public_ip_address_id          = azurerm_public_ip.hello-terraform.id
  }
  tags = var.tags
}
```
Now run the `terraform plan` and then `terraform apply` to create your network.

For the next change, we'll need to run a `terraform destroy` so we can recreate our VM with two NICs, one private and the another one with the public ip attached to it.

At the `vm.tf` module edit the VM resource block to use the public NIC you've just created, you need to comment the private NIC reference line and add this line, it should look like this:
```sh
  # network_interface_ids = [azurerm_network_interface.hello-terraform-private.id] #1. example with vm using the private NIC
  network_interface_ids            = [azurerm_network_interface.hello-terraform-private.id, azurerm_network_interface.hello-terraform-public.id]
  primary_network_interface_id     = azurerm_network_interface.hello-terraform-public.id
```
Now run the `terraform plan` and then `terraform apply` to create your network.

### This is the current setup
![This is the current setup](/img/vm-example7.jpg)

After you applied the output should contain the Public IP allocated to your vm, you can see the username and password were declared at the variables.tf file under the resources `admin_user` and `admin_password`, run the command below:
```sh
ssh azureuser@<publicIp>
```

Once you got ssh access to your VM, please run:
```sh
sudo apt update; sudo apt install nginx -y
```

After installing NGINX in your VM it's time to make some tests. First, from inside your VM run `curl localhost -m 1` you should see the basic page of NGINX, something like:

```sh
<!DOCTYPE html>
<html>
<head>
<title>Welcome to nginx!</title>
<style>
    body {
        width: 35em;
        margin: 0 auto;
        font-family: Tahoma, Verdana, Arial, sans-serif;
    }
</style>
</head>
<body>
<h1>Welcome to nginx!</h1>
<p>If you see this page, the nginx web server is successfully installed and
working. Further configuration is required.</p>

<p>For online documentation and support please refer to
<a href="http://nginx.org/">nginx.org</a>.<br/>
Commercial support is available at
<a href="http://nginx.com/">nginx.com</a>.</p>

<p><em>Thank you for using nginx.</em></p>
</body>
</html>
```

Now let's try to get the same page from outside the VM, try `curl http://<publicIP> -m 2` from your local machine, the publicIp is the same IP you used to SSH into the VM. It should hang for 2 seconds and then fail with a timeout message like this
```sh
curl: (28) Connection timed out after 1003 milliseconds
```

That is happening because the VM security group only allows connections to the port `22`, we declared that at the `"azurerm_network_security_rule" "SSH"` Terraform block in the `vm.tf`, in order to allow connections to the port `80` we also need to add a specific security rule for HTTP.

At the `vm.tf` module add the following block of code to create the a network security group rule that allows HTTP connections from * on the 80 port:
```sh
resource "azurerm_network_security_rule" "HTTP" {
  name                        = "HTTP"
  priority                    = 1000
  direction                   = "Inbound"
  access                      = "Allow"
  protocol                    = "Tcp"
  source_port_range           = "*"
  destination_port_range      = "80"
  source_address_prefix       = "*"
  destination_address_prefix  = "*"
  resource_group_name         = data.azurerm_resource_group.main.name
  network_security_group_name = azurerm_network_security_group.hello-terraform.name
}
```

Run `terraform apply` again to update the Security Group with the HTTP connection rule. 
Once the changes are applied it should take a few moments for Azure to allow traffic to come in and then you can run `curl http://<publicIP> -m 1` again and see the same NGINX hello-world page.

Also test it on your browser: `http://<publicIp>/`


### This is how the final setup looks like
![This is how the final setup looks like](/img/vm-example8.jpg)